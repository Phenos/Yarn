
Volume 3 - The Woods

Book 1 - Highway

Highway is a room. "[one of]That was the hard part. From here on it's a straight shot, no problem.[paragraph break][or][stopping]The grocery store is north, across the highway; that's where you just were. To the south is a trail through the woods, and on the other side of that is your house[one of].[paragraph break]You should try to get there quick, before the storm catches up with you[or][stopping]."

The invisible house is scenery in highway. Instead of doing something with the invisible house, say "You can't see your house from here. It's on the other side of the woods."

The road is scenery in highway. Understand "highway" and "street" and "intersection" and "car" and "cars" as the road. the description of the road is "The cars are all inching along carefully, and the snow on most of the road is undisturbed."

Instead of searching the road, say "The people in the cars are difficult to see, shrouded by clouds of their own breath."

The traffic signal is scenery in highway. Understand "traffic light" as the traffic signal. The description of the traffic signal is "Green, amber, red, green. You should get a move on."

The grocery store is scenery in highway. Understand "market" and "supermarket" and "light" and "lights" and "windows" and "window" as the grocery store. The description of the grocery store is "The grocery store is nearly abandoned; sensible people are all safe in their homes right now. But it is open, and its bright white lights shine out from its plate glass windows as a welcoming beacon to everyone who needs to stock up on bottled water before the blizzard hits."

The streetlamps are plural-named scenery in highway. Understand "streetlight" and "streetlights" and "streetlamp" as the streetlamps. The description of the streetlamps is "Rows of streetlamps hang above the road in both directions, neither of which is the direction of your house."

The trailhead is scenery in highway. Understand "trail" as the trailhead. The description of the trailhead is "As snow falls through the trees and blankets the forest, the trail gets more difficult to discern."

The treeline is a backdrop. The treeline is in highway and nearwoods and nearshed. Understand "tree" and "trees" and "forest" and "wood" and "woods" as the treeline. The description of the treeline is "[if location is highway]From here, the forest seems to go on forever—but it's really not that far to the other side[otherwise]It's getting harder to tell where the forest is. It is probably north of you[end if]."

Instead of climbing the treeline, say "That is not part of your skillset."

Instead of going north in Highway, say "It's too late to go back to the store. [if the package of hot dogs is exed]You'll have to go home without buns[otherwise]Whatever you forgot, you can live without[end if]."

Instead of going nowhere in Highway, say "The way through the woods is south."

Instead of going south in Highway:
	say "Now you are walking into the woods.[paragraph break]Now you are losing your bearings.[paragraph break]You turn around, to retrace your path and return to the highway—but the snow has already covered your footsteps, and the trail (if it was indeed the trail) is invisible.[paragraph break]Now you have no idea where you are.[paragraph break]";
	wander around;
	now visibility-text is "Pretty Bad".

Book 2 - Wandering the Woods

Definition: a room is vacant if it does not enclose the player.

Liwoods is a list of rooms that varies.

To wander around:
	if bearings is true:
		say "Now that you have a compass, you can't really wander anymore—not in earnest, anyway.";
		rule succeeds;
	if liwoods is empty:
		repeat with loco running through rooms in woods:
			add loco to liwoods;
		if location is in woods:
			remove location from liwoods;
	sort liwoods in random order;
	let target be entry 1 of liwoods;
	remove target from liwoods;
	now player is in target.

Woods is a region.

Bearings is a truth state that varies. Bearings is false.

Instead of going nowhere in woods:
	if bearings is false:
		say "You wander [one of]aimlessly[or]determinedly[or]desperately[or]randomly[at random] [one of]through the woods[or]in a direction[or]across the snow[or]along what may very well be the right trail[at random].";
		wander around;
	otherwise:
		let lemmy be a random room adjacent to location;
		let way be the best route from location to lemmy;
		say "The way out of the woods is [way]. Further dillydallying is inadvisable.";

After looking in woods:
	if bearings is false:
		say "[one of]Your house is south, but you don't know which way south is. [or][stopping]Obvious exits are [wandertype], [wandertype], [wandertype], and [wandertype].";
		if remainder after dividing turn count by 7 is 0:
			say "[line break][one of]You could move around more methodically if you had a compass, but you don't have a compass[or]You could conceivably make a compass[or]A compass is just a magnetized needle that's floating on something. Should be easy[stopping].";
	otherwise:
		let lemmy be a random room adjacent to location;
		let way be the best route from location to lemmy;
		say "[one of]The trail is starting to become more obvious, now that you have your bearings. You can continue [way][or]You're pretty sure the way home is [way][or]The trail continues [way][stopping].";

To say wandertype:
	say "[one of]WANDER AIMLESSLY[or]WANDER DETERMINEDLY[or]WANDER DESPERATELY[or]WANDER RANDOMLY[in random order]"

Section 1 - Wandering the actions

Before wanderly behavior:
	if location is in woods:
		continue the action;
	if location is whiteness:
		continue the action;
	otherwise:
		say "You have a fairly solid grasp on your current location and, for better or worse, can't wander effectively." instead.

Wandering is an action applying to a topic. Understand "wander [text]" and "go [text]" as wandering.

Wandering is wanderly behavior.

Instead of wandering in woods:
	if bearings is true:
		say "Now that you have a compass, you can't really wander anymore—not in earnest, anyway.";
		rule succeeds;
	let N be the topic understood;
	if N matches the regular expression "ly\b":
		say "You wander [the topic understood] [one of]through the woods[or]in a direction[or]across the snow[or]along what may very well be the right trail[at random].";
	otherwise:
		say "You wander [one of]aimlessly[or]determinedly[or]desperately[or]randomly[at random] [one of]through the woods[or]in a direction[or]across the snow[or]along what may very well be the right trail[at random].";
	wander around;

Vague wandering is an action applying to nothing. Understand "wander" and "meander" as vague wandering.

Vague wandering is wanderly behavior.

Instead of vague wandering in woods:
	if bearings is true:
		say "Now that you have a compass, you can't really wander anymore—not in earnest, anyway.";
		rule succeeds;
	say "You wander [one of]aimlessly[or]determinedly[or]desperately[or]randomly[at random] [one of]through the woods[or]in a direction[or]across the snow[or]along what may very well be the right trail[at random].";
	wander around;

Random wandering is an action applying to nothing. Understand "wander randomly" and "randomly" as random wandering.

Random wandering is wanderly behavior.

Instead of random wandering in woods:
	if bearings is true:
		say "Now that you have a compass, you can't really wander anymore—not in earnest, anyway.";
		rule succeeds;
	say "You wander randomly [one of]through the woods[or]in a direction[or]across the snow[or]along what may very well be the right trail[at random].";
	wander around;

desperate wandering is an action applying to nothing. Understand "wander desperately" and "desperately" as desperate wandering.

Desperate wandering is wanderly behavior.

Instead of desperate wandering in woods:
	if bearings is true:
		say "Now that you have a compass, you can't really wander anymore—not in earnest, anyway.";
		rule succeeds;
	say "You wander desperately [one of]through the woods[or]in a direction[or]across the snow[or]along what may very well be the right trail[at random].";
	wander around;

determined wandering is an action applying to nothing. Understand "wander determinedly" and "determinedly" as determined wandering.

Determined wandering is wanderly behavior.

Instead of determined wandering in woods:
	if bearings is true:
		say "Now that you have a compass, you can't really wander anymore—not in earnest, anyway.";
		rule succeeds;
	say "You wander determinedly [one of]through the woods[or]in a direction[or]across the snow[or]along what may very well be the right trail[at random].";
	wander around;

aimless wandering is an action applying to nothing. Understand "wander aimlessly" and "aimlessly" as aimless wandering.

Aimless wandering is wanderly behavior.

Instead of aimless wandering in woods:
	if bearings is true:
		say "Now that you have a compass, you can't really wander anymore—not in earnest, anyway.";
		rule succeeds;
	say "You wander aimlessly [one of]through the woods[or]in a direction[or]across the snow[or]along what may very well be the right trail[at random].";
	wander around;



Section 2 - Sounds

Every turn while player is in woods:
	if a random chance of 1 in 8 succeeds:
		say "[one of]You hear a car zooming dangerously fast on the highway—Wait, which way was that coming from?[or]You hear the sirens of a distant emergency vehicle.[or][if corvid encloses the crows]You hear an unseen owl hooting a warning. Too late, owl.[otherwise]You hear a crow shrieking: [fixed letter spacing]'TEKELI		-LI!'[variable letter spacing][line break][end if][stopping]";


Section 3 - Dropping and putting

Instead of dropping something in woods while bearings is false:
	say "You shouldn't drop that here. Who knows if you'd be able to find it again?"

Instead of putting something on something in woods while bearings is false:
	if player encloses second noun:
		say "No need to get fancy.";
	otherwise:
		say "You shouldn't leave that there. Who knows if you'd be able to find it again?"

Book 3 - Backdrops in Woods

The forest is a backdrop. The forest is in woods. Understand "forest" and "woods" and "wood" and "trees" and "branch" and "branches" as the forest.

Understand "tree" as the forest while the player is not enclosed by Corvid.

The description of the forest is "The trees are all essentially identical, and an infinite number of them can seen in any direction. Between their leafless branches the sky glows mercury-pink, swirling with snowflakes."

Instead of climbing the forest, say "That is not part of your skillset."

The freeway is a backdrop. The freeway is in woods. Understand "highway" and "car" and "sirens" and "siren" and "ambulance" and "emergency vehicle" and "road" as the freeway.

Instead of listening to the freeway, say "As the wind picks up, it's tough to make out any sound from the highway."

Instead of doing something other than listening with the freeway, say "You have no idea which way the road is."


Book 4 - Rooms in Woods

Part 1 - Bench

Benchzone is a room in Woods. The printed name of benchzone is "Bench".

The description of benchzone is "There's a little bench here, for hikers to rest on as they enjoy the beauty of nature. It is covered in snow."

The old bench is a scenery supporter in benchzone. The description of the old bench is "It's an old bench, all splintered wood and rusted metal."

Instead of entering the old bench, say "To sit on the bench would be to sit on a bunch of snow. Your butt would get wet."

Instead of looking under the old bench, try examining the old bench.

After examining the old bench the first time:
	now the Altoids tin is in the location;
	say "There's an Altoids tin stuck to the side of it somehow."

Instead of rubbing the old bench, say "By the time you've wiped the snow off the bench, there's a new dusting covering it. This is one of those Sisyphean deals—but you don't have time to rest anyway."


Section 1 - Altoids Tin

The Altoids tin is a closed openable container. The initial appearance is "An Altoids tin is stuck to the side of the bench."

Understand "magnet" and "tape" and "lid" as the tin.

The description of the Altoids tin is "It's obviously an Altoids tin, although it's rusted up pretty bad, and the logo is covered in a bunch of tape, securing [one of]something—oh, a magnet!—[or]a magnet[stopping] to the lid. That must be how [if handled]it was[otherwise]it's[end if] sticking to the side of the bench."

The altoids tin contains a strip of paper. The description of the strip of paper is "[one of]The paper is rolled up tight, but once unrolled, it says:[paragraph break][italic type]Congratulations! You’ve just found a geocache—intentionally or not. Geocaching is the anytime, anywhere adventure where players (called geocachers)[roman type][paragraph break]You don't have time for this. You roll the paper back up[if the altoids tin contains the strip of paper] and return it to the tin[end if][or]You can pick up a new hobby later. Now is not the time for geocaching[stopping]."

The altoids tin contains a penny. The description of the penny is "This penny has seen better days."

Before inserting something into the altoids tin:
	if the noun is the penny or the noun is the strip of paper:
		continue the action;
	otherwise if the noun is the sewing needle:
		say "Once it's left inside the tin, the needle sticks to the side with the magnet.";
		continue the action;
	otherwise if the noun is the opened bottle:
		say "Filling the tin with wine is a losing proposition; the metal is rusted straight through in a few places. Besides, what did you buy those Solo cups for if not to put wine in them?" instead;
	otherwise:
		say "[The noun] won't fit in the Altoids tin. (Altoids tins are very tiny.)" instead;


Part 2 - Crows

Corvid is a room in Woods. The printed name of Corvid is "Tree Full of Crows".

The description of corvid is "You have reached an ancient oak tree. Its branches are heavy with black crows.[paragraph break]They are weirdly quiet."

The oak tree is scenery in corvid.

The description of the oak tree is "The oak dominates this area, forcing shorter and skinnier trees to wait at a respectable distance. Crows are perched on every branch, crowded together in a black mass."

Instead of climbing the oak tree, say "That is not part of your skillset."

The black crows are plural-named scenery in corvid. Understand "crow" and "bird" and "birds" as the black crows.

Instead of doing something other than examining with the black crows, say "They're way up there, and they don't seem to care about anything you do."

The description of the black crows is "The crows are crammed close together for warmth, and you can't make out any individuals, much less guess how many are up there. You can tell they're crows, though, by the way they hop around and flap occasionally.[paragraph break]Here's a fun thing you can try in real life: While you're near a tree full of crows, quickly (but silently) raise your arms into the air.[paragraph break]If the author were going to try doing this in a text adventure interactive documentary, I guess the command he'd enter would be 'wave.'"

Instead of waving at the black crows, try waving hands.

Instead of waving hands while the black crows are visible:
	say "You raise your arms quickly (but silently), and the whole flock of crows flies out of the tree in a noisy and confused manner. As wild as this seems, it is totally a real thing that happens, and in the author's experience it is very reliably reproducible.[paragraph break]It is not typical for the crows to shriek [f]'TEKELI-LI!'[variable letter spacing] as they fly off, though, the way they're doing right now.[paragraph break]Something shiny falls out of the tree and into the snow at your feet. You pick it up before you lose track of it.";
	remove the black crows from play;
	now printed name of corvid is "Empty Oak Tree";
	now the description of corvid is "You have reached an ancient oak tree. Its branches do not have crows hanging out in them anymore.";
	now description of oak tree is "The oak dominates this area, forcing shorter and skinnier trees to wait at a respectable distance. Between its leafless branches the sky glows mercury-pink, swirling with snowflakes.";
	now the player carries the sewing needle.

The sewing needle is a thing. Understand "something" and "shiny" and "something shiny" and "pin" as the sewing needle. The description of the sewing needle is "It's a sewing needle. Apparently someone lost it, then a crow found it, and then the crow lost it—when you scared the poor thing out of its tree."

The sewing needle can be magnetized. The sewing needle is not magnetized.

Instead of putting the sewing needle on the old bench:
	if the needle is magnetized:
		say "You touch the sewing needle to the metal part of the bench. It sticks![paragraph break]You pick up the needle.";
	otherwise:
		say "You press the needle against the bench. Nothing happens."

Instead of putting the sewing needle on the corkscrew:
	if the needle is magnetized:
		say "You press the needle against the corkscrew, and they stick together, tenuously. The curves of the corkscrew make it impossible for the needle to stay stuck for long, so you pull them apart.";
	otherwise:
		say "You touch the needle and the corkscrew to each other. Nothing happens."

Part 3 - Log

Logzone is a room in woods. The printed name of logzone is "Near a Log".

The description of Logzone is "You are near a log. It will serve as a useful point of reference, as long as there's only one fallen log in these woods."

The log is scenery in logzone. The description of the log is "It's a thick log, but the stump it used to be connected to is not in evidence. A layer of snow is rapidly accumulating on top of it."

Instead of entering the log, say "It'd be an uncomfortable seat even if it weren't covered in snow."

Part 4 - Fire Pit

Camp is a room in Woods. The printed name of Camp is "Fire Pit".

The description of camp is "A circle of rocks here indicates that this area was used as a fire pit at some point by an outdoorsperson or outdoorspeople. The rest of their campsite is long gone."

The fire pit is scenery in camp. Understand "camp" and "campsite" and "circle" and "rock" and "rocks" and "circle of rocks" as the fire pit.

The description of the fire pit is "Soon the snow will cover the rocks completely, and the fire pit will appear as a raised circle of snow—then the snow will cover that."

Part 5 - Stump

Stumpzone is a room in Woods. The printed name of stumpzone is "Near a Stump".

The description of stumpzone is "You've stopped near a huge tree stump, which is quickly disappearing under the snow."

The stump is scenery in stumpzone. The description of the stump is "Whatever mighty tree grew from this stump is nowhere to be seen."

Instead of entering the stump, say "It'd be an uncomfortable seat even if it weren't covered in snow."

Book 5 - Escaping The Woods

Understand "rub [the sewing needle] on [the altoids tin]" as putting it on.

Understand "rub [the altoids tin] on [the sewing needle]" as putting it on.

Before putting the altoids tin on the sewing needle:
	say "You rub the needle against the magnetized lid of the tin several times. A dozen times. Twenty times. Forty times.[paragraph break]Eventually your arm gets tired. Well, if it's not magnetized now, it never will be.";
	now the needle is magnetized instead.

Before putting the sewing needle on the altoids tin:
	say "You rub the needle against the magnetized lid of the tin several times. A dozen times. Twenty times. Forty times.[paragraph break]Eventually your arm gets tired. Well, if it's not magnetized now, it never will be.";
	now the needle is magnetized instead.

Understand "magnetize [text]" as a mistake ("One good way to magnetize something is to rub it on a magnet. This only works if the thing you're magnetizing is made out of some kind of ferrous metal in the first place, though.").

Understand "magnetise [text]" as a mistake ("One good way to 'magnetise' something is to rub it on a magnet. This only works if the thing you're 'magnetising' is made out of some kind of ferrous metal in the first place, though.[paragraph break]This interactive documentary takes place in America, by the way.").

Check inserting the sewing needle into the Solo cup:
	say "I'm gonna stop you there. You're on the right track, but I'm afraid the most you can accomplish by putting the needle right in the cup is accidentally swallowing a needle." instead.

Before putting the sewing needle on the cork:
	try inserting the noun into the second noun instead.

Before inserting the sewing needle into the cork:
	if the player does not carry the cork:
		say "[if the Solo cup contains the cork]You fish the cork out of the cup[otherwise]You pick up the cork[end if].";
		silently try taking the cork;

After inserting the sewing needle into the cork:
	say "With a great effort you push the needle lengthwise through the cork, until the point emerges from the other end."

Understand "compass" as the solo cup while bearings is true.

Every turn while bearings is false:
	if the solo cup is full and the solo cup contains the cork:
		if the cork contains the needle and the needle is magnetized:
			now bearings is true;
			map out the woods;
			say "You stare at your makeshift compass intently, shielding it from the snow with your hand, trying not to disturb it with your breath. The cork spins around aimlessly—No! It's turning back![paragraph break]It oscillates, and then it stops, pointing steadfastly toward what must be either the north or the south pole of this Earth. Let's say it's pointing north. It's a 50/50 proposition, right?[paragraph break]You look around. If [italic type]that's[roman type] north, then your house is [italic type]that[roman type] way, and the clearest path through the woods in that general direction is: southeast."

Check examining the solo cup:
	if the solo cup is full and the solo cup contains the cork:
		say "The cup is full of wine; a cork [if the cork contains the needle]with a needle stuck in it [end if]floats on top[if the cork contains the needle and the needle is magnetized], one of its ends pointing north[otherwise if the cork contains the needle], spinning around randomly[end if]."

Check taking the needle:
	if bearings is true and the location is in woods:
		say "You don't want to dismantle your compass before you get out of these dang woods." instead.

Check taking the cork:
	if bearings is true and the location is in woods:
		say "You don't want to dismantle your compass before you get out of these dang woods." instead.

Check dropping the Solo cup:
	if bearings is false:
		say "That would be littering." instead;
	otherwise if location is in woods:
		say "You want to hold onto that compass, at least until you get out of these dang woods." instead;
	otherwise:
		say "Even if you don't need a compass anymore, you don't want to leave a cup of wine sitting around. A kid could find it." instead.

Check inserting the solo cup into something:
	if the solo cup is full:
		say "The cup is full of wine, is the thing." instead;
	otherwise if the second noun is the canvas bag or the second noun is the plastic bag:
		continue the action;
	otherwise:
		try dropping the Solo cup instead.

Check putting the solo cup on something:
	try dropping the Solo cup instead.

A room can be placed or unplaced. A room is usually unplaced.

To map out the woods:
	now the location is placed;
	let larry be a random unplaced room in woods;
	now larry is mapped southeast of the location;
	now larry is placed;
	let iggy be a random unplaced room in woods;
	now iggy is mapped south of larry;
	now iggy is placed;
	let wendy be a random unplaced room in woods;
	now wendy is mapped southwest of iggy;
	now wendy is placed;
	let roy be a random unplaced room in woods;
	now roy is mapped south of wendy;
	now roy is placed;
	now nearwoods is mapped south of roy;


