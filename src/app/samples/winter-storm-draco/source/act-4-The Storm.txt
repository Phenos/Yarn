
Volume 6 - The Storm

Book 1 - Transition Area

Chapter 1 - Peters Street

    Peters Street is south of neargate. Description of peters street is "You are standing up to your ankles in snow just outside the cemetery gate. South of you is Peters Street, a major thoroughfare; a fluorescent-lit pedestrian tunnel leads underneath. To the east is a bus stop.[one of][paragraph break]The thick gusts of snow, now blowing at an extreme angle, make all of this very difficult to see. That won't be a problem for long, though; your house isn't much further.[paragraph break]A white crow flies out of the tunnel.[or][stopping]".

    Every turn while player is in Peters Street:
        now visibility-text is "Terrible";
        now description of sky is "The sky is a turmoil of snow.";
        now description of snow is "On the ground, it sparkles and collects into drifts and blows over itself; in the air, the snow registers only as a fast-moving haze. Its constituent particles are discernible only near a source of light."

    The cemeterywall is a backdrop. The cemeterywall is in Bus Stop and peters street. Understand "wall" and "cemetery wall" and "brick wall" as the cemeterywall. Printed name of cemeterywall is "cemetery wall". Description of cemeterywall is "The snow is so thick now, you can't see the top of the wall."

    Check going north in peters street:
        if gate is open:
            now gate is closed;
            say "As you turn back, the gate swings shut, as if by an invisible hand—no, by the wind." instead;
        otherwise:
            say "The gate is closed." instead.

    The road2 is a backdrop. The road2 is in bus stop and peters street. Printed name of road2 is "Peters Street". Understand "road" and "highway" and "street" and "peters street" and "peters" and "intersection" and "car" and "cars" as the road2. the description of the road2 is "There are no cars on the street. No reasonable person would be out and about in this weather."

    The distant cubicle is scenery in peters street. Understand "bus stop" and "stop" and "booth" as the distant cubicle. Printed name of distant cubicle is "bus stop". Description of distant cubicle is "The bus stop is over to the east."

    Instead of doing something other than examining with the distant cubicle, try examining the distant cubicle.

    The fluorescent lit pedestrian tunnel entrance is scenery in peters street. Printed name of tunnel entrance is "tunnel entrance." Instead of searching tunnel entrance, try examining the noun. Description of tunnel entrance is "You can just discern the light from the tunnel entrance through the snow, but you can't see inside from here."

    Instead of entering tunnel entrance: try going south.

    Instead of going northeast in peters street:
        say "That way is just a brick wall."

    Instead of going northwest in peters street:
        say "That way is just a brick wall."

    Instead of going southeast in peters street:
        say "Although you can't see any cars at the moment, walking down Peters Street is too dangerous. Besides, you're pretty sure your house is south from here."

    Instead of going west in peters street:
        say "Although you can't see any cars at the moment, walking down Peters Street is too dangerous. Besides, you're pretty sure your house is south from here."

    Instead of going southwest in peters street:
        say "Although you can't see any cars at the moment, walking down Peters Street is too dangerous. Besides, you're pretty sure your house is south from here."

    Instead of going down in peters street:
        try going south.

    Instead of going inside in peters street:
        try going south.

    The white crow is scenery in peters street. Understand "bird" and "birds" and "white bird" as the white crow.

    Instead of doing something other than examining with the white crow:
        try examining the white crow.

    Instead of examining the white crow:
        say "It's gone.";
        remove the white crow from play.


Chapter 2 - Bus Stop


    Bus Stop is east of Peters Street. Description of bus stop is "The bus stop is a glass booth, open to the elements on the side facing Peters Street. The entrance to the tunnel is back west."

    Instead of going north in bus stop:
        say "That way is just a brick wall."

    Instead of going northeast in bus stop:
        say "That way is just a brick wall."

    Instead of going northwest in bus stop:
        say "That way is just a brick wall."

    Instead of going southeast in bus stop:
        say "Although you can't see any cars at the moment, walking down Peters Street is too dangerous. Besides, you're pretty sure your house is south from here."

    Instead of going east in bus stop:
        say "Although you can't see any cars at the moment, walking down Peters Street is too dangerous. Besides, you're pretty sure your house is south from here."

    Instead of going southwest in bus stop:
        say "Although you can't see any cars at the moment, walking down Peters Street is too dangerous. Besides, you're pretty sure your house is south from here."

    Instead of going south in bus stop:
        say "You should instead take the tunnel to the west. Crossing Peters Street here would be unnecessarily dangerous, and you do not consider yourself the type of person to do unnecessarily dangerous things."

    The booth is scenery in Bus Stop. Understand "bus stop" and "cubicle" and "stop" as the booth. Description of the booth is "Part of the booth's interior is taken up by a narrow bench: Just wide enough for two people to sit uncomfortably, or for one person to sit while the other person stands and the sitting person wonders what's wrong.[paragraph break]On one of the windows is a rectangular mark: Something was glued to the glass and later ripped down."

    Instead of entering the booth, try entering the narrow bench.

    The narrow bench is an enterable scenery supporter in Bus Stop. The description of the narrow bench is "As of right now there is no snow on it."

    After entering the narrow bench:
        say "You sit down and stare across the street at the snow. The neighborhood on the other side is utterly invisible."

    The window is part of the booth. Understand "glass" and "mark" and "rectangular mark" and "schedule" as the window.

    Instead of searching the window, say "Looks like it's snowing out."

    The description of the window is "There may have been a schedule posted here once. This bus stop may no longer be in operation."

Section 1 - The Phantom Bus

    The phantom is scenery. Understand "bus" and "vehicle" and "car" and "light" and "lights" and "headlights" and "headlight" and "windshield" and "wipers" and "wiper" and "cabin" and "pinprick" and "pinprick of light" and "driver's seat" and "interior" as the phantom.

    Instead of searching the phantom, try examining the noun.

    Schedule is a number that varies.

    Every turn while player is in peters street:
        if schedule is less than 99:
            now schedule is 0.

    Every turn while location is bus stop:
        if schedule is less than 99:
            increment schedule;
        if schedule is 6:
            now phantom is in bus stop;
            now printed name of phantom is "pinprick of light";
            now description of phantom is "You squint, and turn your head from side to side, but the light seems to be real—and it seems to be getting closer.";
            say "Far, far away, in the most distant reaches of Peters Street, a pinprick of light appears.";
        if schedule is 7:
            now printed name of phantom is "headlights";
            now phantom is plural-named;
            now description of phantom is "They must be headlights: Now you can make out the beams of illuminated snow falling in front of them, floating hastily out of the path of whatever vehicle is coming this way.";
            say "As [if the noun is the phantom]it[otherwise]the light[end if] approaches, it resolves in your vision as two lights. Headlights. Maybe.";
        if schedule is 8:
            now printed name of phantom is "bus";
            now description of phantom is "You are certain that it's a bus, even if your certainty is based only on the very vaguely rectangular shape of the object and your increasing desperation.";
            say "[if the noun is the phantom]It's[otherwise]The lights are attached to[end if] something big. Is it a bus? It's got to be a bus. The bus is coming!";
        if schedule is 9:
            now description of phantom is "Each time you think you can discern a detail, a flurry of snow flies through your field of vision, and the bus seems to disappear entirely.";
            say "Now [if the noun is the phantom]it's[otherwise]the bus is[end if] coming into focus, by infinitesimal degrees: The massive windshield, the frantic wipers desperately pushing snow aside, the sickly yellow lights of the cabin.";
        if schedule is 10:
            say "[if the noun is the phantom]But there's no mistaking it now: It's[otherwise]The bus is[end if] almost here, slowing to a stop with some difficulty—The road's very slippery.[paragraph break]From this distance, at least, you can't seem to see any passengers.";
            now description of phantom is "As the bus gets closer, you can see the interior more and more clearly.";
        if schedule is 11:
            say "Now you're sure that there are no passengers. That makes sense; everybody else is at home already. This bus is braving these horrible conditions to save a specific group of very unlucky or very, very stupid people, and it happens that tonight the only person who satisfies any of the criteria is you.[paragraph break]It's really having a lot of trouble slowing down.";
            now description of phantom is "It's still hurtling this way at the same speed—Faster, really, than is prudent in this weather.";
        if schedule is 12:
            say "You realize the bus isn't going to stop. The driver's seat is empty.";
            now description of phantom is "Nobody is driving; nobody is riding.";
        if schedule is 13:
            say "The bus careens heedlessly past the bus stop, and despite its speed you can see inside very clearly, if only for an instant.[paragraph break]Then it's careening away, and all you can see is the rear: Two red lights, one red light, nothing.";
            now schedule is 99;
            remove phantom from play.

    Before going west in bus stop:
        if phantom is in bus stop:
            remove phantom from play;
            say "As you turn away, [the phantom] [if phantom is plural-named]fade[otherwise]fades[end if] into the darkness.";
            now schedule is 100.


    Instead of taking the phantom, say "You mean you want to carry it around? That doesn't make sense."

    Instead of entering the phantom, say "Not while it's moving so fast."

    Instead of going east while phantom is visible:
        say "Walking down Peters Street is too dangerous. Besides, you're pretty sure your house is south from here."

    Instead of going south while phantom is visible:
        say "Crossing the street right now is a terrible idea, what with [if phantom is plural-named]those headlights[otherwise]that [phantom][end if] getting closer and closer."

    Instead of going southwest while phantom is visible:
        say "Crossing the street right now is a terrible idea, what with [if phantom is plural-named]those headlights[otherwise]that [phantom][end if] getting closer and closer."

    Instead of going southeast while phantom is visible:
        say "Crossing the street right now is a terrible idea, what with [if phantom is plural-named]those headlights[otherwise]that [phantom][end if] getting closer and closer."

    Instead of entering road2 while phantom is visible:
        say "That'd be a good way to get run over."


Chapter 3 - Tunnel

    tunnelo is south of peters street. Printed name of tunnelo is "Tunnel". Description of tunnelo is "Fluorescent lights, wreathed with ancient cobwebs, cast the concrete walls with a flickering pallor. Oddly, the light at the southern end of the tunnel is bright white.[paragraph break]A cruel trick of the wind from the northern end blows snow in at an unnatural velocity, with a force that threatens to knock you down."


    The fluorescent lights are plural-named scenery in tunnelo. Understand "light" and "fluorescent light" and "flickering pallor" and "pallor" and "flickering" as the fluorescent lights. The description of the fluorescent lights is "The lights are weak, but you can't look directly at them without allowing ghostly images to remain in your vision, floating across the path ahead."

    The ancient cobwebs are plural-named scenery in tunnelo. Understand "cobweb" and "web" and "webs" and "ancient cobweb" as the ancient cobwebs. The description of the ancient cobwebs is "The wind makes the cobwebs flutter a bit, but it can't dislodge them from the tunnel walls; nothing ever will."

    Instead of rubbing the cobwebs, try taking the noun.

    Instead of pulling the cobwebs, try taking the noun.

    Instead of pushing the cobwebs, try taking the noun.

    Instead of taking the ancient cobwebs, say "You are not wearing gloves."

    The gray concrete walls are plural-named scenery in tunnelo. Understand "wall" and "concrete wall" and "air bubble" and "air bubbles" and "bubble" and "bubbles" and "insignia" and "chiseled insignia" as the concrete walls.

    The description of the concrete walls is "The gray concrete is pockmarked with air bubbles. If you interpret the chiseled insignia correctly, this tunnel was constructed in March of 1687."

    The southern end of the tunnel is scenery in tunnelo. Understand "light" and "white light" and "bright white light" and "white" and "bright white" as the southern end of the tunnel.

    The description of the southern end of the tunnel is "You are unable to guess at the source of the light to the south.[paragraph break]You turn back to the northern end of the tunnel; on that side, the night is utterly black."

    Instead of entering the southern end of the tunnel, try going south.

    The northern end of the tunnel is scenery in tunnelo. Understand "dark" and "black" as the northern end of the tunnel.

    The description of the northern end of the tunnel is "You can infer based on your short-term memory that the northern end of the tunnel leads back to the cemetery, but from here you can't see any such thing."

    Instead of entering the northern end of the tunnel, try going north.

    Instead of going outside in tunnelo, say "Here 'outside' could mean either north or south."

    Instead of exiting in tunnelo, say "Here 'exiting' could mean heading either north or south."

    Every turn while player is in tunnelo:
        now visibility-text is "Fine";
        now description of sky is "You can't see the sky from down here.";
        now description of snow is "Snow skitters in around your feet, settling briefly in wind-shaped curls and then blowing away.";
        if current action is not looking:
            if a random chance of 1 in 2 succeeds:
                say "[one of]A shriek echoes: [fixed letter spacing]'TEKELI-LI!'[variable letter spacing][paragraph break]You turn your head back and forth, but can't tell from which direction it came[or]That cry again: [fixed letter spacing]'TEKELI-LI!'[variable letter spacing]—Then the howling of the wind[stopping]."


Book 2 - Whiteness

Part 1 - Whiteness the Room

    Whiteness is a room.

    Instead of going south in tunnelo:
        now player is in whiteness;
        now description of sky is "You cannot distinguish the sky from the snow.";
        now description of snow is "Snow is all you can see in any direction, at any distance. You can observe snow from every possible perspective, but without a frame of reference, your only perception is that of colorlessness[if draco is visible].[paragraph break]The figure is here, too, but he is also snow[end if]."

Chapter 1 - Welcome to Whiteness

    Description of whiteness is "[one of]You exit the tunnel into absolute whiteness.[paragraph break]A blast of wind hits you in the back and you fall to your knees. Your Solo cup compass flies from your hand.[paragraph break]A gout of wine stains the snow[or]It is completely white[stopping]." Printed name of whiteness is "".

    Instead of looking in whiteness:
        say "[description of whiteness][paragraph break]"

Chapter 2 - Sense of Self

    Instead of examining the player in whiteness:
        say "You are more or less distinct from your environment."

    Instead of taking inventory in whiteness:
        say "You are conscious of a weight pulling down on your arms, although that might just be your hands."

    Before examining in whiteness:
        if player encloses the noun:
            say "You're pretty sure it's still there." instead.

    Before dropping something in whiteness:
        if player encloses the noun:
            say "To release your grip you'd have to be able to feel your hands." instead.

    Before taking something in whiteness:
        if player encloses the noun:
            say "You already have that. Probably." instead.

    Instead of general digging in whiteness:
        say "Your hands aren't up to it, assuming you still have hands."

    Instead of digging something in whiteness:
        say "Your hands aren't up to it, assuming you still have hands."

    Instead of jumping in whiteness:
        say "You can barely stand. Maybe you can jump a little after you're done freezing to death."

    Instead of exiting in whiteness:
        say "You do remember falling down, but currently you seem to be on your feet."


Chapter 4 - The Wine Falls

    Every turn while player is in whiteness:
        remove the Solo cup from play;
        remove the needle from play;
        remove the cork from play.

    The gout of wine is scenery in whiteness. The description of the gout of wine is "The snow covers it immediately."

    After examining the gout of wine:
        remove the noun from play.


Chapter 5 - Screwing with the status line

    Rule for constructing the status line while player is in whiteness:
        center "[white status]" at row 1;
        rule succeeds.

    White status is text that varies. White status is "Location: Unknown ||| Visibility: None".

    Fakenum is a number that varies. Fakenum is 99.

    Chanco is a number that varies. Chanco is 1.

    Every turn while player is in whiteness:
        if fakenum is 99:
            now fakenum is 0;
        otherwise if fakenum is less than 99:
            increment chanco;
            now fakenum is 0;
            destroy white status.

    To destroy white status:
        increment fakenum;
        if a random chance of 2 in 7 succeeds:
            replace character number fakenum in white status with " ";
        if chanco is 7:
            now white status is "";
            now fakenum is 999;
        if fakenum is greater than 36:
            rule succeeds;
        otherwise:
            destroy white status.



Part 2 - Winter Storm Draco

Chapter 1 - It Comes

    Instead of wanderly behavior in whiteness:
        remove the gout of wine from play;
        if draco is visible:
            if draco is faraway:
                say "You push your legs through the snow, either toward or away from the figure. He remains as immense as ever; he does not notice you.";
            otherwise:
                say "[one of]You move away, but the figure remains near. [fixed letter spacing]'Where do you think you're going?'[variable letter spacing] he asks. [fixed letter spacing]'What makes you think there's anywhere else to go?'[variable letter spacing][line break][or]You plow through more and more snow, in every possible direction, but the figure is always nearby.[stopping]";
        otherwise:
            say "You step forward, [one of]through wind that seems to blow in all directions[or]through an atmosphere that is mostly snow[or]through infinite formlessness[stopping].";
        if Draco is in highway:
            now Draco is in Whiteness;
            say "[line break]In the whiteness you can discern a tall white figure.";
        otherwise if Draco is offstage:
            now draco is in highway.


    Instead of going nowhere in whiteness:
        remove the gout of wine from play;
        if draco is visible:
            if draco is faraway:
                say "You set out in a direction, but your only frame of reference is the figure—omnipresent and infinitely distant. Maybe you haven't moved at all.";
            otherwise:
                say "[one of]With no compass, no frame of reference, you only have your own confidence to tell you that you're walking in the direction you believe you're walking in.[paragraph break][fixed letter spacing]'You're leaving me? I'm hurt. I thought we had a real chemistry.'[variable letter spacing][paragraph break]When you look back up, he looms over you from the same distance. Did he follow you, or have you not moved at all?[or]You step resolutely forward, careful to move in a straight line.[paragraph break]It's no use. He's still right there.[stopping]";
        otherwise:
            say "[one of]But which way is that?[paragraph break]You take a few uncertain steps[or]You trudge onward[or]You step forward into identical whiteness[stopping].";
        if Draco is in highway:
            now Draco is in Whiteness;
            say "[line break]In the whiteness you can discern a tall white figure.";
        otherwise if Draco is offstage:
            now draco is in highway.

Chapter 2 - Facts About Draco

    The Winter Storm Draco is a man. Understand "him" and "man" and "tall" and "tall figure" and "tall white figure" and "figure" and "white figure" as Winter Storm Draco.

    Printed name of draco is "figure".

    Draco can be faraway. Draco is faraway.

    Draco can be passive. Draco is passive.

    Description of Draco is "[if Draco is faraway]If the figure is as far off as he seems to be, then he is very tall indeed—but you have the uneasy feeling that he is closer than he looks[otherwise]He is too immense for you to view all of him at once; the snow is too thick for you to see any of him clearly[end if]."

    Instead of taking draco, say "I don't suppose he would care for that."

    Instead of searching draco, say "Looking through him is as futile as looking at him."

    Instead of looking under draco, say "Good luck with that."

    Instead of giving something to draco, try showing the noun to draco.

    Instead of showing something to draco, say "He appears uninterested.[paragraph break]That's not quite right; he doesn't really appear any particular way."

    Instead of kissing draco, say "Snow tickles and melts on your yet-warm lips."

    Instead of touching draco, say "You can't feel any such thing."

    Instead of attacking Draco, say "You're barely able to put one foot in front of the other; you hardly have the strength to win another minigame."

    [talking to him has no effect, so you have to wave.]

    To say he is far away:
        say "The wind swallows your words before they reach the distant figure[one of][or]. There must be some other way to get his attention[then at random]"

    Instead of answering draco that something while Draco is faraway:
        say "[he is far away]."

    Instead of asking draco about something while Draco is faraway:
        say "[he is far away]."

    Instead of telling draco about something while Draco is faraway:
        say "[he is far away]."

    Instead of screaming while draco is visible and draco is faraway:
        say "[he is far away]."


    Understand "talk to [winter storm draco]" as a mistake ("[if draco is faraway][he is far away].[otherwise]He glares down at the source of your failing voice, and then he pointedly breaks eye contact. [fixed letter spacing]'I'm sorry,'[variable letter spacing] he says, [fixed letter spacing]'was there something you wanted to ask me about?'[variable letter spacing][end if]").



    Every turn while Winter Storm Draco is visible:
        if Draco is passive:
            now draco is not passive;
        otherwise:
            if a random chance of 1 in 2 succeeds:
                say "[one of]A puff of steam escapes his nostrils[or]He slicks back his bleached hair. He adjusts his garment[or]Dazzling light glitters on his scales; you have to look away—There's nowhere else to look[or]He strokes his chin[or]He admires his claws[if draco is faraway]; he has not noticed you[end if][then at random].";
            otherwise if a random chance of 1 in 3 succeeds:
                say "[fixed letter spacing]'TEKELI-LI!'[variable letter spacing][line break]"

    The nostrils are a plural-named part of draco. Understand "nose" and "nostril" and "snout" as the nostrils. Description of the nostrils is "His snout is a long point, glittering with scales—No, a cruel hook, which draws back his lips in a permanent sneer."

    The neck is part of draco. Description of the neck is "You can't get a good look."

    The bleached hair is part of Draco. The description of the bleached hair is "Sunburned hair, parted loosely over a brow furrowed with ancient disdain."

    Draco wears the garment. Understand "robe" and "robes" and "toga" and "shoulder" as the garment. The description of the garment is "He wears a long robe—Or a toga, displaying the white sphere of his shoulder."

    The scales is a plural-named part of Draco. Understand "scale" and "skin" as the scales. The description of the scales is "White scales, like a million knights['] pointed shields, cover the entirety of his undulating body, or else his skin is whiter and smoother than a marble statue's."

    The chin is part of Draco. The description of the chin is "He may have a cleft chin. He may not. It is hard to tell under these conditions, and you are slowly freezing to death."

    The claws is a plural-named part of Draco. Understand "hand" and "hands" and "claw" and "talon" and "talons" as the claws. The description of the claws is "His hands are claws; his claws are hands."

    The eyes are a plural-named part of Draco. Understand "eye" as the eyes. The description of the eyes is "His eyes, when you can discern them, are as white as the rest of him, as white as everything else in this universe."

Chapter 3 - We Get His Attention

    Instead of waving at Winter Storm Draco:
        try waving hands.

    Instead of waving something while Winter Storm Draco is visible:
        try waving hands.

    Instead of waving hands while Winter Storm Draco is visible:
        if Draco is faraway:
            say "You lift your arm.[paragraph break]This is more difficult than it usually is.[paragraph break]But the motion gets his attention, and he turns, meets your eye, slithers near.[paragraph break][fixed letter spacing]'Sorry, idiot. I beat you here. Too bad! Your friends are gonna——Say, what's your name?'[variable letter spacing][line break]";
            now command prompt is "Please enter your full name. >";
            now Draco is not faraway;
            now Draco is passive;
        otherwise:
            say "You wave. He rolls his eyes. [fixed letter spacing]'You have my attention.'[variable letter spacing][line break]"

    To decide whether collecting names:
        if the command prompt is "Please enter your full name. >", yes;
        no.

    The player's forename is a text that varies. The player's full name is a text that varies.

    After reading a command when collecting names:
        if the number of words in the player's command is greater than 5:
            say "[paragraph break]He sighs. 'Who are you, a member of the British royal family? No one has that many names. Let's try this again.'";
            reject the player's command;
        now the player's full name is the player's command;
        now the player's forename is word number 1 in the player's command;
        now the command prompt is ">";
        say "[line break][fixed letter spacing]'Nice to meet you, [player's forename in title case]. So the tombstone will read: ['][Player's full name in title case], died December 20, 2012,['] and then in smaller text it'll say [']A Huge Idiot Who Was Too Cool To Wear Gloves.['] That's assuming anybody cares that much about your pathetic frozen corpse. Maybe they'll just let the birds take care of you.'[variable letter spacing] He looks up at the sky. [fixed letter spacing]'No, they've all flown south for the winter!'[variable letter spacing][paragraph break]You hear a [fixed letter spacing]'TEKELI-LI!'[variable letter spacing] from far off, and he grins.[paragraph break][fixed letter spacing]'Well, I'm here for the duration, so I'll try to comfort you in your final moments. You're welcome.'[variable letter spacing][line break]";
        reject the player's command.

    [get his attention, figure out that he's draco, trick him into saying which way is northwest]

Chapter 4 - Draco Resists Being Named

    Instead of naming draco with something:
        if draco is faraway:
            say "He's too far away to be named.";
        otherwise:
            now draco is passive;
            say "He squints at you. [fixed letter spacing]'I already have a name,'[variable letter spacing] he mutters."


Chapter 5 - Talking to Draco


Section 1 - Screaming

    Instead of screaming while draco is visible and draco is not faraway:
        say "You scream. He doesn't seem to notice."

Section 2 - Stupid Rule

    After reading a command when Draco is visible:
        if player's command includes " him", replace the matched text with " figure".

Section 3 - Conversation

    Instead of asking Draco about a topic listed in the Table of Draco Topics:
        now draco is passive;
        say "[reply entry][paragraph break]"

    Instead of asking Draco about something:
        say "He waves the notion away. [fixed letter spacing]'[one of]Next topic[or]Such things are beneath me[or]Boring[then at random].'[variable letter spacing][line break]"

    Instead of telling Draco about something:
        try asking the noun about it.

    Instead of answering Draco that something, say
        "He glares down at the source of your failing voice, and then he pointedly breaks eye contact. [fixed letter spacing]'I'm sorry,'[variable letter spacing] he says, [fixed letter spacing]'was there something you wanted to ask me about?'[variable letter spacing][line break]"

    Instead of answering Draco that "[draco]", try asking the noun about "[draco]".

    // todo: Figure out how to create "table structues"
    // todo: Figure out if table structures are a good idea
    // todo: Would series, sets or lists be more appropriate ?
    Table of Draco Topics
    topic	reply
    "[figure]"	"[one of]His nostrils flare, and fangs appear behind his twisted lips. [fixed letter spacing]'Do you not know who I am? Are you really so stupid?'[variable letter spacing][paragraph break]But he calms himself. [fixed letter spacing]'Of course you are. My mistake. All the same, maybe we should pursue a different topic of conversation--Until you're able to refer to me with a modicum of respect.'[variable letter spacing][or][fixed letter spacing]'Please,'[variable letter spacing] he says, raising a hand in warning, [fixed letter spacing]'We can't begin to talk about me. You have no idea what I am. You don't even know my name.'[variable letter spacing][stopping]"
    "[crows]"	"He halfheartedly stifles a chuckle.[paragraph break][fixed letter spacing]'My opinion is that you ought to treat crows with greater respect. I suppose I should say [']you ought to have treated.['] Your lack of civility hardly matters now, though, tense and aspect notwithstanding.'[variable letter spacing]"
    "[player]"	"[fixed letter spacing]'You? Your name is [player's full name in title case]; you decided (for whatever reason) against taking a bus or a taxi home from the grocery store; you cobbled together an exceedingly primitive compass. Then there was some brutish behavior in a cemetery, which wasted valuable energy that would have been better saved for use here, as you slowly succumb to hypothermia.'[variable letter spacing][paragraph break]He shrugs. [fixed letter spacing]'The rest of the story isn't really about you, per se. You and your dead body are equally unlucky but distinct entities.'[variable letter spacing]"
    "[jacket]"	"[fixed letter spacing]'I was wondering about that myself.'[variable letter spacing] He looks you over. [fixed letter spacing]'I was wondering whether you really thought you'd be warm enough in just that jacket, with no gloves, no hat, no long underwear, or whether perhaps you wore it as a display of flagrant disrespect. Which is it? Impudence or idiocy?'[variable letter spacing][paragraph break]Before you can answer, he hisses: [fixed letter spacing]'Both.'[variable letter spacing]"
    "[wine]"	"[fixed letter spacing]'Oh, please. I have no patience for your little corporeal entertainments. I know humans used to pour wine out onto any old thing, in an attempt to appease the gods. Which you did, a little ways back there, right? But guess what? I am not appeased.'[variable letter spacing]"
    "[hot dogs]"	"[fixed letter spacing]'I did notice your highly sophisticated taste, but I'm afraid food isn't an interest of mine. Eating is more of a human thing.'[variable letter spacing]"
    "[storm]"	"[fixed letter spacing]'[']Storm!['] What do you mean, [']Storm?['] Don't you watch the news? We're not talking about any old storm.'[variable letter spacing] He pulls at his hair in consternation, and mutters something you can't hear."
    "snow"	"He looks around. [fixed letter spacing]'Pretty, right? I mean, I'm used to it. But I know humans have a thing where they think anything that can kill them is beautiful.'[variable letter spacing]"
    "wind"	"[fixed letter spacing]'What wind?'[variable letter spacing] He smirks until you're about to speak again, and then he interrupts you. [fixed letter spacing]'I don't think much about it, is the thing. I just go where it takes me.'[variable letter spacing]"
    "blizzard"	"[fixed letter spacing]He inhales through his fangs, exasperated. [fixed letter spacing]'Yes, a blizzard. A big, bad blizzard. A blizzard with a name.'[variable letter spacing]"
    "[woods]"	"[fixed letter spacing]'Yes, yes, I saw you dithering around in the woods. It was a tedious sight, until I realized you were really trying to find a way out; then it was hysterical.'[variable letter spacing]"
    "tekeli-li"	"[fixed letter spacing]'No. You're saying it wrong.'[variable letter spacing] He curves his serpentine neck and whistles: [fixed letter spacing]'TEKELI-LI! Like that. If you can't say it right, I'm not gonna tell you what it means.'[variable letter spacing]"
    "[compass]"	"[fixed letter spacing]'Oh, you want directions? Why don't you build another compass? You're not getting any help from me.'[variable letter spacing]"
    "[cemetery]"	"[fixed letter spacing]'Yes, I recall watching you stumble through the cemetery. I saw you nearly trip over about a hundred tombstones. Then [if the shovel is handled]you picked up that shovel, then [end if]you fought that Black Angel, then you ended up out here. [']Good Job,['] is that what I'm supposed to say? No. A good job would be staying inside, where it's warm.'[variable letter spacing]"
    "[bus stop]"	"[fixed letter spacing]'[if bus stop is visited]I did see you hanging around the bus stop, like an idiot. [end if]No, the buses aren't running any longer. You had your chance, but you said [']No, I'll walk!['] Congratulations.'[variable letter spacing][if schedule is 99][paragraph break]You ask him about the bus you saw.[paragraph break]He frowns. [fixed letter spacing]'Uh, no. I don't remember anything like that.'[variable letter spacing]"
    "[black angel]"	"He rolls his eyes and blows a puff of air up into his bangs. [fixed letter spacing]'I'm afraid I can't speak for her. Maybe she was trying to protect you from me.'[variable letter spacing] He chuckles. [fixed letter spacing]'That doesn't sound much like her, does it?'[variable letter spacing]"
    "[name]"	"He drums his fingers for a bit before he speaks. [fixed letter spacing]'I know you humans give each other names like candy canes at Christmas, but for a guy like me to have a name is kind of a big deal. So you can understand my frustration, knowing that you apparently haven't heard of me.[paragraph break]'But you won't be around for long, right? And everyone else--everyone who matters--they know me, and they'll remember me. You should be so lucky.'[variable letter spacing]"
    "[draco]"	"He bows deeply, approximating a demure expression. [fixed letter spacing]'Yes, that's me. And I'd be happy to tell you everything about myself, or as much as I can fit in before you die of exposure. Let's see.'[variable letter spacing][paragraph break]He looks through the snow and points a claw into the distance. [fixed letter spacing]'I brought myself into existence off the western coast of North America a few days ago. I crossed the Rockies without difficulty and, having accumulated a profound amount of atmospheric moisture, I started to cover the middle of your continent with rain, snow, thunder and lightning, everything. I just got here tonight, but I'd say I've already made quite an impression.[paragraph break]'It was never my intention to hurt anybody; I get nothing out of inflicting pain. But I do tend to think your sad fate might not be all that pointless. Those who hear about what happened to you--if you're remembered at all--will hopefully take the story to heart, and it may encourage them to take me seriously.[paragraph break]'From here I'm heading east: Boston, D.C., New York City. They'll appreciate me over there. For you Middle America yokels, every snowstorm's the same. A mild inconvenience! [']Uh-oh, we have to shovel the sidewalk again!['] But they'll really get me. They'll be talking about me for years: [']Winter Storm Draco.['][paragraph break]'Are you dead yet?'[variable letter spacing][paragraph break]No, you're thinking. If west is [italic type]that[roman type] way...[orient player]"

    Oriented is a truth state that varies. Oriented is false. [Refers specifically to loss of orientation in Whiteness.]

    To say orient player:
        now draco is proper-named;
        now printed name of draco is
            "Winter Storm Draco";
        now oriented is true.


    Before going south in whiteness:
        if oriented is true:
            say "You turn away from Winter Storm Draco and begin walking south.[paragraph break][fixed letter spacing]'Hey! Where are you going?'[variable letter spacing][paragraph break]A scrap of color flickers in the air ahead. Your eyes are playing tricks on you.[paragraph break][fixed letter spacing]'Hey, idiot! Get back here! You--'[variable letter spacing][paragraph break]His voice cracks, replaced by screeching and flapping so loud as to drown out even the wind. You break into a run.";
            now visibility-text is "Complicated";
            now description of snow is "The snow has stopped falling. It rests in a solid, perfectly level mass on top of every available surface.";
            now description of sky is "The sky, still a uniform mass of cloud, is now an odd pink color. The significance of this is unclear.";
            now player is in Euclid Street instead.

    Instead of going nowhere in whiteness while oriented is true:
        say "You cast an eye that way, hoping that some landmark will appear from out of the snow and confirm your sense of place, but nothing materializes. If you do know north from south, though, there's only one way to go."


