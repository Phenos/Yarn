### The Plastic Bag

    // todo: Should there be a "carries" predicate to mimic this syntax ? (which could convert to "is in")
     The player carries the plastic bag.

    Section 1 - The Hot Dogs

        The plastic bag contains the package of hot dogs.
        The package of hot dogs is important.

        The description of the package of hot dogs is
            "This package contains twelve hot dogs. You don't know how long this blizzard will last,
            but you and your housemates probably cannot survive the whole storm on twelve hot
            dogs[one of].[paragraph break]It occurs to you that you forgot the buns[or][stopping]."

        Instead of opening the hot dogs, say
            "It's plenty cold out here, but it'd be more sanitary to leave the package sealed until
            you could put it in a proper refrigerator."

        Instead of eating the hot dogs, say
            "You should save those for when you're starving to death."



    Section 2 - The Solo Cups


        The plastic bag contains the package of Solo cups. The package of solo cups is important.

        The Solo cups can be closed or open. The solo cups is closed.

        Instead of taking the solo cups while the solo cups is closed:
            try opening the solo cups.

        The description of the package of Solo cups is
            "This package contains [if open]71[otherwise]72[end if] red Solo cups.
             You and your housemates have plans for these."

        Instead of opening the solo cups the first time:
            now the solo cups is open;
            now the player carries the Solo cup;
            say "You tear the plastic apart and pull out a cup."

        Instead of opening the solo cups:
            say "It's already open. You've already got a cup."

        Instead of closing the solo cups, say
            "[if the solo cups is open]The plastic is torn; you can't reseal
             it[otherwise]It's pretty well closed already[end if]."

        Instead of inserting something into the solo cups, say
            "It'd be more practical to use a single cup as a container."



    Section 3 - The Single Cup


        The Solo cup is a container.
        The description of the Solo cup is "It's a red plastic cup[if full] with some wine in it[end if]."

        The Solo cup can be full. The Solo cup is not full.


        Instead of inserting the solo cup into the solo cups, say
            "Too late! The cup is out."

        Instead of inserting a bottle into the Solo cup:
            if the noun is not the opened bottle:
                say "The bottle isn't open.";
            otherwise if the Solo cup is full:
                say "There's already some wine in there." instead;
            otherwise:
                now the Solo cup is full;
                say "You pour some wine into the Solo cup.";

        Instead of drinking the Solo cup:
            if the noun contains something:
                say "Well, there's [a list of things in the solo cup] in there right now.";
            otherwise if the noun is full:
                say "You take a leisurely sip, and it warms your insides—a little.";
            otherwise:
                say "That cup is empty."


        Before inserting the snow into the Solo cup: say
            "A cup of snow won't do you much good. A cup of water might be more useful,
             but melting that much snow would require more body heat than you can spare."
                instead.



### Corkscrew

    The plastic bag contains the corkscrew.
    The corkscrew is important.
    The description of the corkscrew is
        "It recently came to light that, somehow, neither you nor any of your housemates owned
         a corkscrew. Well, from now on, any time they want to screw a cork out of anything,
         they'll be at your mercy."

    Instead of unlocking something with the corkscrew, try opening the noun.



### The Canvas Bag

    The player carries the canvas bag.
    A bottle is a kind of thing.
    A bottle is usually important.


### The Wine

    The canvas bag contains two bottles.

    The description of a bottle is usually
        "This is some extremely cheap wine."

    The opened bottle is a bottle.
    The printed name of the opened bottle is
        "open bottle of wine".

    The unopened bottle is a bottle.
    The printed name of the unopened bottle is
        "unopened bottle of wine".

    // Todo: whoa... say whaaa ?
    Instead of opening a bottle:
        if the opened bottle is offstage:
            repeat with vino running through bottles:
                remove vino from play;
            now player carries the opened bottle;
            now the unopened bottle is in the canvas bag;
            now the player carries the cork;
            say "You apply the corkscrew with some difficulty, given how cold your hands are and
                how cold everything else around is. But then there's a [i]pop![roman type]—and you
                have a loose cork and an open bottle on your hands.";
        otherwise:
            say "You've already opened a bottle[one of][or]. This is not the party you
                think it is[stopping]."

    Instead of drinking a bottle:
        if the noun is not the opened bottle:
            say "That bottle is not open.";
        otherwise:
            say "[one of]Mmm. Wine-y[or]Dook dook, yum yum[or]
                That might be enough for right now[stopping]."

    //todo: Mechanics for pouring wine in solo cups
    The Solo cup can be full. The Solo cup is not full.

    //todo: Solocup is Drinkable only when they are full
    Does the player mean drinking the Solo cup:
        if the solo cup is full:
            it is very likely;
        otherwise:
            it is very unlikely.

    // todo: what is this supposed to do ?
    /*
    Before inserting a bottle (called boite) into the package of solo cups:
        if the package of solo cups is closed:
            try opening the package of solo cups;
            try inserting boite into the solo cup instead.
    */




### The cork

    The cork is a container.
    The description of the cork is
    "It's a sad little cork, with no insignia. One end is stained purple."
